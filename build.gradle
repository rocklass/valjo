plugins {
	id "net.saliman.cobertura" version "2.2.7"
	id "com.github.kt3k.coveralls" version "2.3.1"
	id "net.researchgate.release" version "2.0.2"
	id "com.jfrog.bintray" version "1.0"
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "maven-publish"

sourceCompatibility = 1.8

group = "org.rocklass"

jar {
	doFirst {
	    manifest {
	        attributes 	"Implementation-Title": title,
	        			"Implementation-Version": version
	    }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile	"junit:junit:$junitVersion"
    testCompile "org.apache.commons:commons-lang3:$commonsLang3Version"
    testCompile "net.sourceforge.cobertura:cobertura:$coberturaVersion"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

// update version during release (no snapshot)
confirmReleaseVersion << {
    publishing.publications.mavenJava.version = project.version
    jar.version = project.version
    sourcesJar.version = project.version
    publishing.publications.mavenJava.setArtifacts([jar, sourcesJar])
    bintrayUpload.versionName = project.version
    bintrayUpload.versionVcsTag = project.version
}

uploadArchives {
    repositories {
       flatDir {
           dirs "repository"
       }
    }
}

release {
	failOnPublishNeeded = false
	tagPrefix = project.name
}

createReleaseTag.dependsOn bintrayUpload

bintray {
    user = project.hasProperty("bintrayUser") ? project.getProperty("bintrayUser") : "" // from GRADLE_USER_HOME/gradle.properties
    key = project.hasProperty("bintrayKey") ? project.getProperty("bintrayKey") : "" // from GRADLE_USER_HOME/gradle.properties

    publications = ["mavenJava"]
    dryRun = false
    publish = true
    pkg {
        repo = "maven"
        name = project.name
        desc = "valjo (VALue Java Object)"
        websiteUrl = "https://github.com/rocklass/valjo"
        issueTrackerUrl = "https://github.com/rocklass/valjo/issues"
        vcsUrl = "https://github.com/rocklass/valjo.git"
        licenses = ["GPL-3.0"]
        publicDownloadNumbers = true
        version {
                name = project.version
                vcsTag = project.version
        }
    }
}
